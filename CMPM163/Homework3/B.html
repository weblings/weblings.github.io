
<!DOCTYPE html>
<html lang="en">
	<head>
		<title>demo - shader</title>
		<meta charset="utf-8">
		<style>
			body {
			  margin: 0px;
				overflow: hidden;
			}
		</style>
	</head>
	<body>

		<div id="container"></div>

    <script src="js/three.js"></script>
		

    <script id="vertexShader" type="x-shader/x-vertex">

	uniform mat4 modelMatrix;
	uniform mat4 viewMatrix;
    uniform mat4 projectionMatrix;

    attribute vec3 position;
	attribute vec2 uv;
	attribute vec3 normal;


    precision mediump float;


	void main() {
       
       		gl_Position = projectionMatrix  * viewMatrix * modelMatrix  * vec4( position, 1.0 );
        }

	</script>

	<script id="fragmentShader" type="x-shader/x-fragment">

precision mediump float;

uniform vec2 u_resolution;


float boxDist(vec2 p, vec2 size)
{
	vec2 d = abs(p) - size;
  	return min(max(d.x, d.y), 0.0) + length(max(d, 0.0));
}

float circleDist(vec2 p, float radius)
{
	return length(p) - radius;
}

float triangleDist(vec2 p, float width, float height)
{
	vec2 n = normalize(vec2(height, width / 2.0));
	return max( abs(p).x*n.x + p.y*n.y - (height*n.y), -p.y );
}

float merge(float d1, float d2)
{
	return min(d1, d2);
}


vec2 translate(vec2 p, vec2 t)
{
	return p - t;
}

float sphereSDF(vec3 p) {
    return length(p) - 1.0;
}

vec3 translate3D(vec3 p, vec3 t){
  return p - t;
}

float sdTorus( vec3 p, vec2 t )
{
  vec2 q = vec2(length(p.xz)-t.x,p.y);
  return length(q)-t.y;
}

void main() {

    float x_inc = u_resolution.x / 4.0;
    float y_pos = u_resolution.y / 2.0;

    vec2 centerPtA = vec2(x_inc*1.0, y_pos);
    vec2 centerPtB = vec2(x_inc*2.0, y_pos);
    vec2 centerPtC = vec2(x_inc*3.0, y_pos - u_resolution.y/6.0 );
    vec3 samplePoint = vec3(centerPtC,0.0);//1.2,1.2,1.2);

    float distA = sphereSDF(samplePoint/1.2) * 1.2;
    //float distA = sdCylinder( vec3(0,0,0), vec3(100,100,100)); 
    //float distA = sdCylinder( translate3D( vec3(gl_FragCoord.xy, 0.0), vec3(centerPtA, 0.0)), vec3(100,100,100)); 
    //float distA = sdCylinder( vec3(centerPtA, 0.0), vec3(100,100,100)); 
    //boxDist(  translate(gl_FragCoord.xy, centerPtA), vec2(100,100));
    float distB = circleDist(  translate(gl_FragCoord.xy, centerPtB), 100.0 );
    float distC = triangleDist(  translate(gl_FragCoord.xy, centerPtC), 250.0, 230.0 );

    /*if (distA > 1.0) {
    	gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0);
    } else */
    if(distA < 0.0){
        gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0);
    }else{
        gl_FragColor = vec4(1.0, 1.0, 0.0, 1.0);
    }
    
    /*if (distB < 0.0) {
    	gl_FragColor = vec4(0.0, 1.0, 0.0, 1.0);
    } else*//* if (distC < 0.0) {
    	gl_FragColor = vec4(0.0, 0.0, 1.0, 1.0);
    } else {
    	gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
    }*/
    

}
	</script>
        
    <script id="shaderToyFragmentShader" type="x-shader/x-fragment">
    
    precision mediump float;
    uniform float iTime;
    uniform vec2 u_resolution;

        /**
 * Part 5 Challenges:
 * - Change the axis of motion of the cube being intersected with the sphere
 * - Rotate the intersection 
 */

const int MAX_MARCHING_STEPS = 255;
const float MIN_DIST = 0.0;
const float MAX_DIST = 100.0;
const float EPSILON = 0.0001;

/**
 * Constructive solid geometry intersection operation on SDF-calculated distances.
 */
float intersectSDF(float distA, float distB) {
    return max(distA, distB);
}

/**
 * Constructive solid geometry union operation on SDF-calculated distances.
 */
float unionSDF(float distA, float distB) {
    return min(distA, distB);
}

/**
 * Constructive solid geometry difference operation on SDF-calculated distances.
 */
float differenceSDF(float distA, float distB) {
    return max(distA, -distB);
}

/**
 * Signed distance function for a cube centered at the origin
 * with width = height = length = 2.0
 */
float cubeSDF(vec3 p) {
    // If d.x < 0, then -1 < p.x < 1, and same logic applies to p.y, p.z
    // So if all components of d are negative, then p is inside the unit cube
    vec3 d = abs(p) - vec3(1.0, 1.0, 1.0);
    
    // Assuming p is inside the cube, how far is it from the surface?
    // Result will be negative or zero.
    float insideDistance = min(max(d.x, max(d.y, d.z)), 0.0);
    
    // Assuming p is outside the cube, how far is it from the surface?
    // Result will be positive or zero.
    float outsideDistance = length(max(d, 0.0));
    
    return insideDistance + outsideDistance;
}

/**
 * Signed distance function for a sphere centered at the origin with radius 1.0;
 */
float sphereSDF(vec3 p) {
    return length(p) - 1.0;
}

//Andrew: IQ functions
float sdCylinder( vec3 p, vec3 c )
{
  return length(p.xz-c.xy)-c.z;
}

/**
 * Signed distance function describing the scene.
 * 
 * Absolute value of the return value indicates the distance to the surface.
 * Sign indicates whether the point is inside or outside the surface,
 * negative indicating inside.
 */
 
 //Andrew: Cool rotating sphere thing
 /*float sceneSDF(vec3 samplePoint) {
    float sphereDist = sphereSDF(samplePoint / 1.2) * 1.2;
    float cylDist = sdCylinder(samplePoint, vec3(sin(iTime),0,cos(iTime)));
    return intersectSDF(cylDist, sphereDist);
}*/

float sdTorus( vec3 p, vec2 t )
{
  vec2 q = vec2(length(p.xz)-t.x,p.y);
  return length(q)-t.y;
}
 
/*float sceneSDF(vec3 samplePoint) {
    //float sphereDist = sphereSDF(samplePoint / 1.2) * 1.2;
    float torusDist = sdTorus(samplePoint, vec2(0.0, sin(iTime/3.0) * 3.0));
    float cubeDist = cubeSDF(samplePoint + vec3(0.0, sin(iTime), 0.0));
    return intersectSDF(cubeDist, torusDist);//sphereDist);
}*/

float sceneSDF(vec3 samplePoint) {
    //float sphereDist = sphereSDF(samplePoint / 1.2) * 1.2;
    //float torusDist = sdTorus(samplePoint, vec2(sin(iTime/3.0) * 3.0, sin(iTime/3.0) * 3.0));
    float torusDist = sdTorus(samplePoint, vec2(0.0, sin(iTime/3.0) * 3.0));
    float cubeDist = cubeSDF(samplePoint + vec3(0.0, sin(iTime), 0.0));
    return intersectSDF(cubeDist, torusDist);//sphereDist);
}

/**
 * Return the shortest distance from the eyepoint to the scene surface along
 * the marching direction. If no part of the surface is found between start and end,
 * return end.
 * 
 * eye: the eye point, acting as the origin of the ray
 * marchingDirection: the normalized direction to march in
 * start: the starting distance away from the eye
 * end: the max distance away from the ey to march before giving up
 */
float shortestDistanceToSurface(vec3 eye, vec3 marchingDirection, float start, float end) {
    float depth = start;
    for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
        float dist = sceneSDF(eye + depth * marchingDirection);
        if (dist < EPSILON) {
			return depth;
        }
        depth += dist;
        if (depth >= end) {
            return end;
        }
    }
    return end;
}
            

/**
 * Return the normalized direction to march in from the eye point for a single pixel.
 * 
 * fieldOfView: vertical field of view in degrees
 * size: resolution of the output image
 * fragCoord: the x,y coordinate of the pixel in the output image
 */
vec3 rayDirection(float fieldOfView, vec2 size, vec2 fragCoord) {
    vec2 xy = fragCoord - size / 2.0;
    float z = size.y / tan(radians(fieldOfView) / 2.0);
    return normalize(vec3(xy, -z));
}

/**
 * Using the gradient of the SDF, estimate the normal on the surface at point p.
 */
vec3 estimateNormal(vec3 p) {
    return normalize(vec3(
        sceneSDF(vec3(p.x + EPSILON, p.y, p.z)) - sceneSDF(vec3(p.x - EPSILON, p.y, p.z)),
        sceneSDF(vec3(p.x, p.y + EPSILON, p.z)) - sceneSDF(vec3(p.x, p.y - EPSILON, p.z)),
        sceneSDF(vec3(p.x, p.y, p.z  + EPSILON)) - sceneSDF(vec3(p.x, p.y, p.z - EPSILON))
    ));
}

/**
 * Lighting contribution of a single point light source via Phong illumination.
 * 
 * The vec3 returned is the RGB color of the light's contribution.
 *
 * k_a: Ambient color
 * k_d: Diffuse color
 * k_s: Specular color
 * alpha: Shininess coefficient
 * p: position of point being lit
 * eye: the position of the camera
 * lightPos: the position of the light
 * lightIntensity: color/intensity of the light
 *
 * See https://en.wikipedia.org/wiki/Phong_reflection_model#Description
 */
vec3 phongContribForLight(vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye,
                          vec3 lightPos, vec3 lightIntensity) {
    vec3 N = estimateNormal(p);
    vec3 L = normalize(lightPos - p);
    vec3 V = normalize(eye - p);
    vec3 R = normalize(reflect(-L, N));
    
    float dotLN = dot(L, N);
    float dotRV = dot(R, V);
    
    if (dotLN < 0.0) {
        // Light not visible from this point on the surface
        return vec3(0.0, 0.0, 0.0);
    } 
    
    if (dotRV < 0.0) {
        // Light reflection in opposite direction as viewer, apply only diffuse
        // component
        return lightIntensity * (k_d * dotLN);
    }
    return lightIntensity * (k_d * dotLN + k_s * pow(dotRV, alpha));
}

/**
 * Lighting via Phong illumination.
 * 
 * The vec3 returned is the RGB color of that point after lighting is applied.
 * k_a: Ambient color
 * k_d: Diffuse color
 * k_s: Specular color
 * alpha: Shininess coefficient
 * p: position of point being lit
 * eye: the position of the camera
 *
 * See https://en.wikipedia.org/wiki/Phong_reflection_model#Description
 */
vec3 phongIllumination(vec3 k_a, vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye) {
    const vec3 ambientLight = 0.5 * vec3(1.0, 1.0, 1.0);
    vec3 color = ambientLight * k_a;
    
    vec3 light1Pos = vec3(4.0 * sin(iTime),
                          2.0,
                          4.0 * cos(iTime));
    vec3 light1Intensity = vec3(0.4, 0.4, 0.4);
    
    color += phongContribForLight(k_d, k_s, alpha, p, eye,
                                  light1Pos,
                                  light1Intensity);
    
    vec3 light2Pos = vec3(2.0 * sin(0.37 * iTime),
                          2.0 * cos(0.37 * iTime),
                          2.0);
    vec3 light2Intensity = vec3(0.4, 0.4, 0.4);
    
    color += phongContribForLight(k_d, k_s, alpha, p, eye,
                                  light2Pos,
                                  light2Intensity);    
    return color;
}

/**
 * Return a transform matrix that will transform a ray from view space
 * to world coordinates, given the eye point, the camera target, and an up vector.
 *
 * This assumes that the center of the camera is aligned with the negative z axis in
 * view space when calculating the ray marching direction. See rayDirection.
 */
mat4 viewMatrix(vec3 eye, vec3 center, vec3 up) {
    // Based on gluLookAt man page
    vec3 f = normalize(center - eye);
    vec3 s = normalize(cross(f, up));
    vec3 u = cross(s, f);
    return mat4(
        vec4(s, 0.0),
        vec4(u, 0.0),
        vec4(-f, 0.0),
        vec4(0.0, 0.0, 0.0, 1)
    );
}

void main()//{//Image( out vec4 fragColor, in vec2 fragCoord )
{
	vec3 viewDir = rayDirection(45.0, u_resolution.xy, gl_FragCoord.xy);//fragCoord);
    vec3 eye = vec3(8.0, 5.0, 7.0);
    
    mat4 viewToWorld = viewMatrix(eye, vec3(0.0, 0.0, 0.0), vec3(0.0, 1.0, 0.0));
    
    vec3 worldDir = (viewToWorld * vec4(viewDir, 0.0)).xyz;
    
    float dist = shortestDistanceToSurface(eye, worldDir, MIN_DIST, MAX_DIST);
    
    if (dist > MAX_DIST - EPSILON) {
        // Didn't hit anything
        gl_FragColor = vec4(0.0, 0.0, 0.0, 0.0);
		return;
    }
    
    // The closest point on the surface to the eyepoint along the view ray
    vec3 p = eye + dist * worldDir;
    
    vec3 K_a = vec3(0.2, 0.2, 0.2);
    vec3 K_d = vec3(0.7, abs(cos(iTime * 0.5)), abs(sin(iTime)));
    vec3 K_s = vec3(1.0, 1.0, 1.0);
    float shininess = 10.0;
    
    vec3 color = phongIllumination(K_a, K_d, K_s, shininess, p, eye);
    
    gl_FragColor = vec4(color, 1.0);
}
    </script>
    
	<script>


var container;

var camera, scene, renderer, mesh;

var vs = document.getElementById( 'vertexShader' ).textContent;
var fs = document.getElementById( 'fragmentShader' ).textContent;
var stfs = document.getElementById('shaderToyFragmentShader').textContent;

	var mouseX = 0.0;
	var mouseY = 0.0;

init();
animate();

function init() {

	container = document.getElementById( 'container' );

	var width = window.innerWidth;
	var height = window.innerHeight;


	camera = new THREE.OrthographicCamera( width / -2, width / 2, height / 2, height / -2, 0.1, 1000 );
	camera.position.z = 10;

	scene = new THREE.Scene();


	// geometry

	var geometry = new THREE.PlaneGeometry( width, height, 1, 1 );

	// material

	var uniforms =  {
		u_resolution: { type: "v2", value: new THREE.Vector2(width, height) },
        iTime: {type: "f", value: performance.now()}
	};


	var material = new THREE.RawShaderMaterial( {

		uniforms: uniforms,
		vertexShader: vs,
		fragmentShader: stfs,

	} );


	mesh = new THREE.Mesh( geometry, material );
	scene.add( mesh );


	renderer = new THREE.WebGLRenderer();
	renderer.setSize( window.innerWidth, window.innerHeight );
	container.appendChild( renderer.domElement );


        window.addEventListener( 'resize', onWindowResize, true );
	
			}

			function onWindowResize( event ) {

				camera.aspect = window.innerWidth / window.innerHeight;
				camera.updateProjectionMatrix();

				renderer.setSize( window.innerWidth, window.innerHeight );
				mesh.material.uniforms.u_resolution.value = new THREE.Vector2(window.innerWidth, window.innerHeight);
			}
			

			function animate() {

				requestAnimationFrame( animate );
				render();

			}

			function render() {

                var time = performance.now()/500;
                var object0 = scene.children[ 0 ];
	 			object0.material.uniforms.iTime.value = time;//Math.sin(time * 0.001); //0.01;
				renderer.render( scene, camera );
			}




		</script>

	</body>
</html>

